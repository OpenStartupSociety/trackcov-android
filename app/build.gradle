apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.openstartupsociety.socialtrace"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 3
        versionName "1.0"

        multiDexEnabled true

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = "${applicationId}-${variant.name}-${variant.versionName}-" + new Date().format("dMMM") + ".apk"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            resValue 'string', 'app_name', "SocialTrace"
        }
        debug {
            minifyEnabled true
            shrinkResources true

            ext.enableCrashlytics = false

            versionNameSuffix "-dev"
            applicationIdSuffix ".debug"

            resValue 'string', 'app_name', "SocialTrace Debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    dataBinding {
        enabled = true
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'

    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${lifecycle_version}"
    kapt "androidx.lifecycle:lifecycle-common-java8:${lifecycle_version}"

    def retrofit_version = '2.8.1'
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-moshi:${retrofit_version}"

    def okhttp_version = '4.4.1'
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"

    def moshi_version = '1.9.2'
    implementation "com.squareup.moshi:moshi-kotlin:${moshi_version}"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:${moshi_version}"

    implementation 'com.google.firebase:firebase-auth:19.3.0'
    implementation 'com.google.firebase:firebase-analytics:17.2.3'
    implementation 'com.google.firebase:firebase-messaging:20.1.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta02'
    implementation 'com.google.firebase:firebase-config-ktx:19.1.3'

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def dagger_version = '2.27'
    api "com.google.dagger:dagger:${dagger_version}"
    api "com.google.dagger:dagger-android:${dagger_version}"
    api "com.google.dagger:dagger-android-support:${dagger_version}"
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"
    kapt "com.google.dagger:dagger-android-processor:${dagger_version}"

    implementation 'com.google.android.gms:play-services-nearby:17.0.0'
    implementation 'io.coil-kt:coil:0.9.5'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.github.mukeshsolanki:android-otpview-pinview:2.1.0'
    implementation('com.journeyapps:zxing-android-embedded:3.6.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.0'

    testImplementation 'junit:junit:4.13'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
